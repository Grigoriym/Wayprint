import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    `kotlin-dsl`

    // Mirrors the root `build.gradle.kts`'s own minimal shape — `configureLinting()` is defined
    // inside this project, so `build-logic` can't apply it to itself.
    alias(libs.plugins.detekt)
    alias(libs.plugins.ktlint)
}

group = "com.grappim.wayprint.buildlogic"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
    }
}

tasks {
    validatePlugins {
        enableStricterValidation = true
        failOnWarning = true
    }
}

dependencies {
    compileOnly(libs.android.gradlePlugin)
    compileOnly(libs.kotlin.gradlePlugin)
    compileOnly(libs.compose.gradlePlugin)
    compileOnly(libs.compose.multiplatform.gradlePlugin)
    compileOnly(libs.detekt.gradlePlugin)
    compileOnly(libs.ktlint.gradlePlugin)
    compileOnly(libs.kover.gradle.plugin)
}

gradlePlugin {
    plugins {
        register("androidApplication") {
            id = libs.plugins.wayprint.android.application.get().pluginId
            implementationClass = "AndroidApplicationConventionPlugin"
        }
        register("kmpLibrary") {
            id = libs.plugins.wayprint.kmp.library.asProvider().get().pluginId
            implementationClass = "KmpLibraryConventionPlugin"
        }
        register("kmpLibraryCompose") {
            id = libs.plugins.wayprint.kmp.library.compose.get().pluginId
            implementationClass = "KmpLibraryComposeConventionPlugin"
        }
        register("kmpLibraryStability") {
            id = libs.plugins.wayprint.kmp.library.stability.get().pluginId
            implementationClass = "KmpLibraryStabilityConventionPlugin"
        }
        register("kmpSerialization") {
            id = libs.plugins.wayprint.kmp.serialization.get().pluginId
            implementationClass = "KmpSerializationConventionPlugin"
        }
        register("kmpDi") {
            id = libs.plugins.wayprint.kmp.di.get().pluginId
            implementationClass = "KmpDiConventionPlugin"
        }
    }
}
